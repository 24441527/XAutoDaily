apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlinx-serialization'

apply from: './versioning.gradle'

def signingFilePath = './keystore.gradle'
def performSigning = file(signingFilePath).exists()
if (performSigning) {
    apply from: signingFilePath
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"
    ndkVersion '21.4.7075529'

    defaultConfig {
        applicationId "me.teble.xposed.autodaily"
        minSdkVersion 24
        //noinspection OldTargetApi
        targetSdkVersion 30
        versionCode mVersionCode
        versionName "3.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11"
            }
        }
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
    }

    if (performSigning) {
        signingConfigs {
            release {
                storeFile file(project.signing.storeFilePath)
                storePassword project.signing.storePassword
                keyAlias project.signing.keyAlias
                keyPassword project.signing.keyPassword
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (performSigning) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (performSigning) {
                signingConfig signingConfigs.release
            }
            versionNameSuffix ".$mBuildNum-debug"
        }
        alpha {
            initWith release
            shrinkResources false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (performSigning) {
                signingConfig signingConfigs.release
            }
            matchingFallbacks = ['release']
            versionNameSuffix ".$mBuildNum-alpha"
        }
        rc {
            initWith release
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (performSigning) {
                signingConfig signingConfigs.release
            }
            matchingFallbacks = ['release']
            versionNameSuffix "-rc1"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.6.0"
        }
    }

    aaptOptions {
        additionalParameters '--allow-reserved-package-id', '--package-id', '0x62'
    }
    lintOptions {
        abortOnError false
    }

    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion kotlin_version
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "XAutoDaily-${versionName}.apk"
        }
    }
}

dependencies {
//    implementation project(":stub")
    implementation fileTree(dir: "libs", include: ["*.jar"])
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"

    // Xposed
    implementation "com.github.kyuubiran:EzXHelper:0.6.1"
    compileOnly "de.robv.android.xposed:api:82"

    // qq stub
    compileOnly files("lib/qqStub.jar")

    // Sdk
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.5"

    // jetpack compose
    implementation "androidx.activity:activity-compose:1.4.0"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.navigation:navigation-compose:2.4.0-rc01"

    // hutool
    def hutool_version = "5.7.15"
    implementation "cn.hutool:hutool-core:$hutool_version"
    implementation "cn.hutool:hutool-cron:$hutool_version"
    implementation "cn.hutool:hutool-http:$hutool_version"
    implementation "cn.hutool:hutool-crypto:$hutool_version"

    // Other
    implementation "com.tencent:mmkv-static:1.2.10"
    implementation "net.bytebuddy:byte-buddy-android:1.12.7"
    implementation "com.charleskorn.kaml:kaml:0.36.0"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.0"
    implementation "org.apache-extras.beanshell:bsh:2.0b6"
    implementation 'com.jayway.jsonpath:json-path:2.6.0'
}